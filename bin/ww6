#!/usr/bin/env perl6
#
# The script to manage ww6 sites.
#

use File::Mkdir;
use File::Find;

## Returns true if the OS is not Windows.
sub not_win {
  $*VM<config><osname> ne 'MSWin32';
}

## chmod, is completely ignored on Windows.
sub chmod($file, $mode) {
  if not_win() {
    run "chmod $mode $file";
  }
}

sub show-dir ($listdir) { 
  my @temps = dir($listdir);
  for @temps -> $dir {
    my $space = ' ' x 11-$dir.chars;
    print ' '~$dir~$space;
    say slurp($listdir~'/'~$dir~'/desc.txt');
  }
}


sub show-types ($resdir) {
  my $tempdir = $resdir~'/templates';
  my $daemdir = $resdir~'/daemons';

  say "List of currently available templates:";
  show-dir($tempdir);
  say "List of currently available daemon scripts:";
  show-dir($daemdir);

  exit;
}

sub resdir {
  return $*PROGRAM_NAME.subst('ww6', '../share/ww6');
}

## Create a new site, by default creates it in the current folder.
## The current hackish place for the resource directory is in ~/.perl6/share
## It finds it by looking where 'ww6' is (generally ~/.perl6/bin) and setting
## the resdir to ../share/ww6 from that path.

multi sub MAIN (:$create!, :$dir='.', :$daemon='default') {
  my $resdir = resdir();
  my $tdir   = $resdir~'/templates/'~$create;
  my $dfile  = $resdir~'/daemons/'~$daemon~'/daemon.pl6';

  if $create ne '' && $tdir.IO ~~ :d && $daemon ne '' && $dfile.IO ~~ :f {
    print "Creating a new '$create' in '$dir'... ";
    for find(dir => $tdir, type => 'file').list -> $file {
      my $target-dir = $file.dir.subst($tdir, $dir);
      mkdir $target-dir, :p;
      if not_win() {
        run "cp $file $target-dir/{$file.name}";
      }
      else {
        run "copy $file $target-dir/{$file.name}";
      }
    }
    if not_win() {
      run "cp $dfile $dir/";
    }
    else {
      run "copy $dfile $dir/";
    }
    say "done.";
  }
  else {
    say "Invalid 'create' or 'daemon' type specified.";
    show-types($resdir);
  }
}

## Show the types

multi sub MAIN (:$list!) {
  my $dir = resdir();
  show-types($dir);
}

